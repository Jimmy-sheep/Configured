plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${project.mod_id}-fabric"
version = "${project.mod_version}-${project.minecraft_version}"
group = project.maven_group

sourceSets {
	testMods {
		compileClasspath += test.compileClasspath
		runtimeClasspath += test.runtimeClasspath
	}
	main {
	}
}

loom {
	accessWidenerPath = file("src/main/resources/configured.accesswidener")

	runs {
		testClient {
			inherit client
			name = "Test Client"
			programArg "--username=Dev"
			programArg "--uuid=380df991f603344ca090369bad2a924a"
			ideConfigGenerated = true
			source = sourceSets.testMods
		}
		testServer {
			inherit server
			name = "Test Server"
			ideConfigGenerated = true
			source = sourceSets.testMods
		}
	}
}

repositories {
    maven {
        // location of the maven that hosts JEI files before January 2023
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
        url "https://maven.terraformersmc.com/"
    }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	mappings loom.officialMojangMappings()

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "com.terraformersmc:modmenu:5.0.2"

	// JEI
	modCompileOnlyApi "mezz.jei:jei-${project.minecraft_version}-common-api:${project.jei_version}"
	modCompileOnlyApi "mezz.jei:jei-${project.minecraft_version}-fabric-api:${project.jei_version}"
	modRuntimeOnly "mezz.jei:jei-${project.minecraft_version}-fabric:${project.jei_version}"

	// Nullable annotation
	compileOnly "com.google.code.findbugs:annotations:3.0.1"

	// Add night config toml
	implementation include('com.electronwill.night-config:core:3.6.0')
	implementation include('com.electronwill.night-config:toml:3.6.0')

	testModsImplementation sourceSets.main.output
}

processResources {
	inputs.property "version", project.mod_version

	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.mod_id}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = "${project.mod_id}-fabric"
			from components.java
		}
	}

	repositories {
		maven {
			url "file:///X:/localmaven/mcmods"
		}
	}
}
